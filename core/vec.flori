
import "core/prelude"
import "core/pointer"
import "core/io"
import "core/syntax"
import "core/macros"
import "core/region"

type Vec[T] $[nodestruct] {
  data WPtr[T]
  cap Int
  len Int
}

fn vec[T](len Int, cap Int) Vec[T] $[importjs, patternjs "new Array($1)"] {
  var v Vec[T]
  v.data = alloc_with[T](cap)
  v.cap = cap
  v.len = len
  v
}
fn vec[T](len Int) Vec[T] {
  vec[T](len, len * 2)
}
fn vec[T]() Vec[T] {
  vec[T](0, 8)
}

fn copy[T](r ref Region, v Vec[T]) Vec[T] {
  newv := vec[T](v.len, v.cap)
  copy_memory(to_ptr(newv.data), to_ptr(v.data), sizeof[T]() * v.len)
  newv
}

macro vec(elems TArray) FExpr $[syntax] {
  if (length(elems) == 0) {
    error(elems, "vec syntax require arguments.")
  }
  ret := new_fblock()
  tmp := gensym()
  t := get_type(elems!0)
  push(ret, quote {
      `tmp := vec[`t]()
    })
  for (e : elems) {
    push(ret, quote {
        push(`tmp, `e)
      })
  }
  push(ret, tmp)
  ret
}

fn extend[T](v ref Vec[T]) {
  realloc(v.data, v.cap*2)
  v.cap = v.cap*2
}

fn push[T](v ref Vec[T], value T) $[nodestruct, importjs, patternjs "$1.push($2)"] {
  if (v.cap < v.len+1) {
    extend(v)
  }
  unref(to_ptr(v.data) +! (v.len * sizeof[T]())) = value
  v.len += 1
}

fn length[T](v Vec[T]) Int $[importjs, patternjs "$1.length"] {
  v.len
}

fn `![T](v Vec[T], i Int) ref T $[importjs, patternjs "$1[$2]"] {
  unref(to_ptr(v.data) +! (i * sizeof[T]()))
}

fn `!![T](v ref Vec[T], i Int, value T) {
  unref(to_ptr(v.data) +! (i * sizeof[T]())) = value
}

iterator items[T](v TExpr[Vec[T]]) {
  i := gensym()
  quote {
    for (`i : range(int(0), length(`v)-1)) {
      `element := `v!`i
      `yield
    }
  }
}

fn print[T](v Vec[T]) {
  print("vec[")
  if (length(v) > 0) {
    print(v!0)
  }
  for (i : range(1, length(v)-1)) {
    print(", ")
    print(v!i)
  }
  print("]")
}

fn getref[T](v ref Vec[T]) Ptr[T] {
  v.data.p
}
