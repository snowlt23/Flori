struct storage {
  p ^pointer
  pos ^int
}

syntax buffer:() ^fmap {
  name := parse()
  size := parse()
  tsym := gensym()
  vsym := gensym()
  varf := fmap("var")
  set(varf, "name", vsym)
  set(varf, "vartype", ftype(tsym))
  namef := fmap("var")
  set(namef, "name", name)
  set(namef, "vartype", ftype(fident("storage")))
  quote {
    defprimitive `dup(tsym)` `size`
    inline fn cast_pointer(p ^ptr `dup(tsym)`) ^pointer {}
    `varf`
    `namef`
    static {
      `dup(name)`.p = cast_pointer(& `dup(vsym)`)
      `dup(name)`.pos = 0
    }
  }
}

macro <@(p ^fmap, v ^fmap) ^fmap {
  if is(p, FMAP_IDENT) {
    quote {
      buffer_at(& `p`, `v`)
    }
  } else {
    quote {
      buffer_at(`p`, `v`)
    }
  }
}

macro at(p ^fmap, t ^fmap, i ^fmap) ^fmap {
  quote {
    *cast_ptr_int(`p`.p + sizeof(`t`) * `i`)
  }
}

fn buffer_at(p ^ptr storage, v ^int) ^ptr storage {
  *cast_ptr_int(p.p + p.pos) = v
  p.pos += sizeof(^int)
  p
}

fn buffer_at(p ^ptr storage, c ^char) ^ptr storage {
  *cast_ptr_char(p.p + p.pos) = c
  p.pos += 1
  p
}

fn buffer_at(p ^ptr storage, s ^cstring) ^ptr storage {
  copy(cast_cstring(p.p + p.pos), s)
  p.pos += len(s)
  p
}