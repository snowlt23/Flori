
import core/prelude
import core/io
import core/macros

macro for(fexpr FExpr) FExpr {
  if ((length(fexpr) != 5) && (length(fexpr) != 6)) {
    error(fexpr, "expect syntax: for e in iter() {...}")
  }
  
  elem := access(fexpr, 1)
  in := access(fexpr, 2)
  if (to_cs(in) != "in") {
    error(fexpr, "for-macro expect `in keyword.")
  }

  itername := access(fexpr, 3)
  iterargs := if (length(fexpr) == 6) {
    access(fexpr, 4)
  } else {
    new_flist()
  }
  body := if (length(fexpr) == 6) {
    access(fexpr, 5)
  } else {
    access(fexpr, 4)
  }

  quote {
    `itername `iterargs `elem `body
  }
}

macro range(fexpr FExpr) FExpr {
  if (length(fexpr) < 4) {
    error(fexpr, "expect syntax: range(s, e) i {...}")
  }
  args := access(fexpr, 1)
  if (length(args) < 2) {
    error(fexpr, "range: require 2 arguments")
  }
  start := access(args, 0)
  end := access(args, 1)
  elem := access(fexpr, 2)
  body := access(fexpr, 3)

  quote {
    `elem := `start
    while (`elem <= `end) {
      `body
      `elem += 1
    }
  }
}
